#!/usr/bin/perl -w -I../commonlib/perllib
#
# test-run:
# Test harness for PledgeBank.  Makes sure we haven't broken the code.
# 
# Requires:
# * ../conf/general file set up for PledgeBank, and matching the below requirements
# * apache configured to serve ../web on OPTION_BASE_URL
# * a database with name ending "_testharness"; this script will drop and remake the
#   database, so make sure it is never used for anything important
# * email addresses (email_n below) configured to pipe to ./test-mailin with fast
#   local delivery
#
# Copyright (c) 2005 UK Citizens Online Democracy. All rights reserved.
# Email: francis@mysociety.org; WWW: http://www.mysociety.org/

# TODO
# Now we have hook in WebTestHarness, make it do error log check automatically
# (at the moment we often get HTML validation errors shown for what are really
# things that are in the error log)
# Add test code for:
#   Prominence
#     Calculated prominence stuff
#     Backpage pledges
#   Search
#   Categories
#   Fuzzy pledge ref match
#   Deleting comments
#   Microsites

#   PDF and RTF stuff from poster.cgi
#   SMS on failure
#   SMS already signed, and SMS am creator so can't sign
#   Remember Me button (how do we test Cookie expirty with WWW::Mechanize?)
#   Picture uploading
#   Info page graph
#   Log in with wrong password
#   Private pledges: info page, announcearchive page

my $rcsid = ''; $rcsid .= '$Id: test-run,v 1.247 2008-01-10 17:26:01 francis Exp $';

use strict;
require 5.8.0;

use Data::Dumper;
use Storable;
use FindBin;
use Getopt::Long;
use Encode;

use mySociety::Config;
mySociety::Config::set_file('../conf/general');
use mySociety::DBHandle qw(dbh);
use mySociety::WebTestHarness;

sub help {
print <<END

Usage: test-run [OPTION] [ACTION]...

Actions are a list of tests, run if present in this order:
    public - make a public succeeding pledge
    private - make a private failing pledge
    chivvy - test sending of chivvy emails
    sms - make a pledge, sign up by SMS and convert
    login - exhaustively test user login / authentication
    category - display of pledges by prominence, category and language
    byarea - test pledges which have target "by area" 
If you specify no actions, it will run all of them, plus some
extra smaller tests.

Options are:
    --verbose=n    Choose 0 (no progress), 1 (basic actions), 2 (full debug)
    --pause        Pause after displaying URLs read from emails
    --multispawn=n Test concurrency by calling scripts this many times at once (default 1)

END
}

# Parse command line
our $verbose = 0; # currently 3 levels: 0, 1 and 2
our $pause = 0;
our $multispawn = 1; # now crontab just runs on one machines anyway, gave up
our $help;
if (!GetOptions(
        'verbose=i' => \$verbose,
        'pause' => \$pause,
        'multispawn=i' => \$multispawn,
        'help' => \$help
    )) {
    help();
    exit(1);
}
if ($help) {
    help();
    exit(0);
}
our %action;
foreach (@ARGV) {
    if ($_ eq "public") {
        $action{'public'} = 1;
    } elsif ($_ eq "private") {
        $action{'private'} = 1;
    } elsif ($_ eq "sms") {
        $action{'sms'} = 1;
    } elsif ($_ eq "chivvy") {
        $action{'chivvy'} = 1;
    } elsif ($_ eq "login") {
        $action{'login'} = 1;
    } elsif ($_ eq "category") {
        $action{'category'} = 1;
    } elsif ($_ eq "byarea") {
        $action{'byarea'} = 1;
    } else {
        help();
        print "Action '$_' not known\n";
        exit(0);
    }
}
if (scalar(@ARGV) == 0) {
    $action{'all'} = 1;
    $action{'public'} = 1;
    $action{'private'} = 1;
    $action{'sms'} = 1;
    $action{'chivvy'} = 1;
    $action{'login'} = 1;
    $action{'category'} = 1;
    $action{'byarea'} = 1;
}

# Set up options

our $base_url = mySociety::Config::get('BASE_URL');
our $stub_url = mySociety::Config::get('WEB_DOMAIN');
if (mySociety::Config::get('WEB_HOST') ne 'www') {
    $stub_url = mySociety::Config::get('WEB_HOST') . "." . $stub_url;
}
our $admin_url = mySociety::Config::get('ADMIN_URL');
our $admin_auth_user = mySociety::Config::get('ADMIN_AUTH_USER');
our $admin_auth_password = mySociety::Config::get('ADMIN_AUTH_PASSWORD');
our $sms_in_url = mySociety::Config::get('PB_SMS_C360_INCOMING_URL');
our $httpd_error_log = mySociety::Config::get('HTTPD_ERROR_LOG');
our $email_domain = mySociety::Config::get('EMAIL_DOMAIN');
our $contact_email = mySociety::Config::get('CONTACT_EMAIL');
our $test_email_prefix = mySociety::Config::get('TEST_EMAIL_PREFIX');
sub email_n { my $n = shift; return "$test_email_prefix+$n\@$email_domain"; }
sub name_n { my $n = shift; return ($n % 100  == 0) ? "Peter Setter $n" : "Siegfried Signer $n"; }
sub mobile_n { my $n = shift; return "9876543$n"; }
sub password_n { my $n = shift; return "mypass$n"; }
sub location_n { my $n = shift; 
    if ($n % 3 == 0) {
        return { country => 'GB', place => 'Hexham' }
    } elsif ($n % 3 == 1) {
        return { country => 'GB', place => 'Liverpool' } 
    } else {
        return { country => 'GB', place => 'Birkenhead' } 
    }
} # for byarea pledge signing
our $login_count; # reference to hash of person number => how many times they've logged in
our $name_admin = "\"PledgeBank.com\"";

#############################################################################
# Main code

# Configure test harness class
date_print("Set up web test harness...");
our $wth = new mySociety::WebTestHarness();
$base_url =~ m#^http://(.+)/?$#;
$wth->browser_credentials("www.$1:80", "$stub_url admin pages", $admin_auth_user, $admin_auth_password);
$wth->browser_credentials("$1:80", "$stub_url admin pages", $admin_auth_user, $admin_auth_password);

# Specify URL as first parameter, will browse to it and store it as the new
# locale base URL to use. Otherwise specify no URL, and the current one
# is used.
our $locale_url = $base_url; # including country code, old language code
our $locale_url_redirected = $base_url; # after country codes are redirected out into cookie, new language code
# Note that WWW::Mechanize 1.19+ changes its behaviour, and will return the *redirected* URL in browser_uri()
sub lock_locale_url { 
    my $new_url = shift;
    $wth->browser_get($new_url) if $new_url;
    $new_url = $wth->browser_uri() if !$new_url;
    $new_url =~ m#^(http://[^/]+)/?#; 
    $locale_url = $1;
    $wth->browser_base() =~ m#^(http://[^/]+)/?#; 
    $locale_url_redirected = $1;
    verbose("lock_locale_url browser URL is $locale_url from $new_url, redirected $locale_url_redirected from " . $wth->browser_base());
}

$wth->database_connect('PB_');
$wth->database_drop_reload('../db/schema.sql');
$wth->database_load_schema('../db/ican-categories.sql');
$wth->database_cycle_sequences(200);
# XXX As services are deployed now for Ratty, comment this out
my $eveld_bin = "$FindBin::Bin/../../services/EvEl/bin/eveld";
$eveld_bin = undef if ! -e $eveld_bin; # when running on servers rely on EvEl daemon, rather than calling EvEl binary directly as on Francis'' laptop XXX need more explicit way of distinguishing this case, than just checking evel isn't checked out in the same tree
$wth->email_setup({ eveld_bin => $eveld_bin,
                    eveld_multispawn => $multispawn,
                    log_mailbox => "log_mailbox" });
#$wth->browser_set_validator("/usr/bin/validate");
sms_setup();

# Syntax check all .php files
date_print("Syntax check all PHP files...");
$wth->php_check_syntax("../../pledgebank/");
$wth->php_check_syntax("../../pledgebank/templates/emails/", qr//);

# Setup error log watching
$wth->log_watcher_setup($httpd_error_log);
$wth->log_watcher_self_test($base_url . "/test.php?error=1", "deliberate_error_to_test_error_handling");

# Run some pledges through life cycle
if ($action{'all'}) {
    date_print("Making unconfirmed pledge...");
    do_unconfirmed_pledge();
}
if ($action{'public'}) {
    date_print("Making public pledge and making it succeed...");
    do_public_succeeding_pledge();
} 
if ($action{'private'}) {
    date_print("Making private pledge and letting it expire...");
    do_private_failing_pledge();
}
if ($action{'chivvy'}) {
    date_print("Making pledge and getting lots of chivvies...");
    do_chivvy_testing();
}
if ($action{'sms'}) {
    date_print("Testing SMS with pledge...");
    do_sms_pledge();
}
if ($action{'login'}) {
    date_print("Testing user login...");
    do_login_tests();
}
if ($action{'category'}) {
    date_print("Testing display of pledges of different types...");
    do_category_tests();
}
if ($action{'byarea'}) {
    date_print("Testing pledges with targets by area...");
    do_byarea_tests();
}
if ($action{'all'}) {
    date_print("Checking administration functions...");
    do_admin_tests();
    date_print("Final checks (RSS etc.)...");
    do_final_checks();
}

# Check for any unhandled mails or errors
call_frequentupdate();
call_send_alerts();
$wth->email_check_none_left(1);
sms_check_none_left();
$wth->log_watcher_check();
if ($action{'all'})  {
    print "Everything completed successfully\n";
} else {
    print "Tests completed successfully\n";
}

#############################################################################
# Functions to make and sign pledges, and so on

# Print log line with date
sub date_print {
    $_ = shift;
    print scalar localtime() . " $_\n";
}

# Print what we're doing
sub comment {
    my $comment = shift;
    date_print("  $comment") if $verbose > 0;
}
sub verbose {
    my $comment = shift;
    date_print("    $comment") if $verbose > 1;
}

# display_url URL
# Print the URL if in verbose mode.  If --pause set, also print it and 
# wait for RETURN to be pressed.
sub display_url {
    my ($circumstance, $url) = @_;
    $wth->email_check_url($url);
    date_print("$circumstance: " . $url . "\n") if $verbose > 1 || $pause;
    if ($pause) {
        print "Press RETURN to continue";
        readline(*STDIN);
    }
}

# perform_login_action WHO LOGIN_OPTIONS EMAIL_TEXT
# Call after doing something which requires login.  LOGIN_OPTIONS defines what
# to do about this.  It is a hashref of values.  They are:
#   "action" which can have the values
#       "viaemail" means request a confirmation email, wait for it, and follow link in it
#       "loggedin" means expect to be already logged in
#       "password" means the user is not logged in but has a password, so log in with it
#       "wrongpassword" means the user is not logged in, try logging in with the wrong password
#       "abandon" means request a confirmation email, check it is there, but don't follow link
#       "suspend" means request a confirmation email, and leave it in mail queue table
#       "resume" means to continue from point of waiting for confirmation mail
#   "setpassword" which applies for "viaemail" and "resume" types and can have the values:
#       0 never to set the user's password
#       1 to set the user's password 
#   "givemail" applies for every type except "resume" and "loggedin"
#       0 the login form does not require email (was entered in an earlier form)
#       1 the login form requires email
#   TODO distinguish viaemail already logged in from not?
# EMAIL_TEXT is an array of SQL LIKE query fragments that confirmation mail must match one of.
sub perform_login_action {
    my ($who, $login_options, $email_text) = @_;
    my $login_action = $login_options->{'action'};
    my $login_fields = $login_options->{'giveemail'} ? { email => email_n($who) } : {};

    verbose("login by ".name_n($who)." action '$login_action' for " . $wth->browser_uri());

    if ($login_action eq "viaemail" || $login_action eq "abandon" || 
        $login_action eq "suspend" || $login_action eq "resume") {

        # Request confirmation email (unless we are resuming request)
        if ($login_action ne "resume" && $login_action ne 'abandon') { # Won't work for SIGNING
            $wth->browser_check_contents(qr/(I've never used PledgeBank before|Nunca usei o EuPrometo antes)/);
            $login_fields->{'loginradio'} = 'SendEmail';
            $wth->browser_submit_form(form_name => 'login',
                fields => $login_fields,
                button => 'loginsubmit') or die "Failed to request confirmation email";
            $wth->browser_check_contents(qr/(you'll need to click the link|precisa clicar para poder)/);
        }

        # Wait for it to arrive, and extract the link (unless we are suspending request)
        my $confirmation_url;
        if ($login_action ne "suspend") {
            my @email_text_array = map { '%To:%'.email_n($who).'%'.$_.'%' } @$email_text;
            my $confirmation_email = $wth->email_get_containing(\@email_text_array);
            die "Pledge confirmation link not found\n" if ($confirmation_email !~ m#^\s*(http://.*$)#m);
            $confirmation_url = $1;
            display_url("Email confirmation URL", $confirmation_url);
        }

        if ($login_action eq "viaemail" || $login_action eq "resume") {
            # Follow link and confirm
            $wth->browser_get($confirmation_url);
            $login_count->{$who}++;
            if ($login_count->{$who} > 0) {
                if ($login_options->{'setpassword'}) {
                    # We use the option to set our password
                    $wth->browser_check_contents("Set a password");
                    $wth->browser_submit_form(form_name => 'setpassword',
                        fields => { pw1 => password_n($who), pw2 => password_n($who) },
                        button => 'submit') or die "Failed to submit no make password form";
                    $wth->browser_check_contents("Password successfully set");
                    # And go back to where we were
                    $wth->browser_back();
                }
            }
        } elsif ($login_action eq "abandon" || $login_action eq "suspend") {
            # Deliberately stop without following link
            return; 
        } else {
            die;
        }
    } elsif ($login_action eq "loggedin") {
        # Nothing to do
    } elsif ($login_action eq "password") {
        $wth->browser_check_contents("I have a PledgeBank <strong>password</strong>");
        $login_fields->{'password'} = password_n($who);
        $login_fields->{'loginradio'} = 'LogIn';
        $wth->browser_submit_form(form_name => 'login',
            fields => $login_fields,
            button => 'loginsubmit') or die "Failed to log in with password";
    } elsif ($login_action eq "wrongpassword") {
        $wth->browser_check_contents("I have a PledgeBank <strong>password</strong>");
        $login_fields->{'password'} = "thispassswordisnonsense";
        $login_fields->{'loginradio'} = 'LogIn';
        $wth->browser_submit_form(form_name => 'login',
            fields => $login_fields,
            button => 'loginsubmit') or die "Failed to try to log in with password";
        $wth->browser_check_contents("Either your email or password weren't recognised");
    } else {
        die "Unknown login action '$login_action'";
    }
}

# create_pledge WHO PAGE1 PAGE2 PAGE3 LOGIN_ACTION
# Creates a new pledge, setter is person number WHO.  PAGE1,
# PAGE2 and PAGE3 contain parameters for each page of the sign-up form.
# LOGIN_ACTION is passed on to perform_login_action.  If PAGE2 contains
# appropriate gaze_place value, that will be used to select which radio
# button in the choice of towns (if your town is unique, it is not needed)
sub create_pledge {
    my ($who, $page1, $page2, $page3, $login_options) = @_;
    my $login_action = $login_options->{'action'};

    verbose("Creating pledge ". $page1->{'ref'});
    $page1->{name} = name_n($who); 
    $page1->{email} = email_n($who);
    die "'resume' not supported for pledges yet" if ($login_action eq "resume");

    # Make new pledge, step 1
    $wth->browser_get($locale_url_redirected); # We need the language, and hopefully not the country!
    $wth->browser_follow_link(text_regex => qr/(Start your pledge|Criar sua promessa)/);
    $wth->browser_check_contents(qr/(New Pledge|nova promessa)/);
    $wth->browser_submit_form(form_name => 'pledge', fields => $page1,
        button => 'tostep2') or die "Failed to submit creating form step 1";
    if ($page1->{target} >= 20) {
        $wth->browser_check_contents("Please lower your target");
        $wth->browser_submit_form(form_name => 'pledge', fields => { target => $page1->{target} },
            button => 'donetargetwarning') or die "Failed to submit target warning";
    }

    # Step 2
    $wth->browser_check_contents(qr/(New Pledge &ndash; Location|Nova Promessa &ndash; Localidade)/);
    my $gaze_place;
    if ($page2->{gaze_place}) {
        $gaze_place = $page2->{gaze_place};
        delete $page2->{gaze_place};
    }
    $wth->browser_submit_form(form_name => 'pledge',
        fields => $page2,
        button => 'tostep3') or die "Failed to submit creating form step 2";
    if ($gaze_place) {
        # If picked textual place, select it
        $page2->{gaze_place} = $gaze_place;
        $wth->browser_submit_form(form_name => 'pledge',
            fields => $page2,
            button => 'tostep3') or die "Failed to submit creating form step 2, with place selected";
    }

    # Step 3
    $wth->browser_check_contents(qr#(New Pledge &ndash; Category / Privacy|Nova Promessa &ndash; Categoria / Privacidade)#);
    $wth->browser_submit_form(form_name => 'pledge',
        fields => $page3,
        button => 'topreview') or die "Failed to submit creating form step 3";

    # Preview
    $wth->browser_check_contents(qr/(Now please read your pledge)|(Agora leia sua promessa)/);
    my $fields = {};
    if ($login_action eq 'password') {
        $fields->{password} = password_n($who);
    }
    $wth->browser_submit_form(form_name => 'pledge',
        fields => $fields,
        button => 'tocreate') or die "Failed to submit creating form step 4";

    # Requires authentication
    if ($login_action eq 'viaemail') {
        $login_options->{action} = 'resume'; # As the new form has done the first bit
    }

    if ($login_action ne 'password') {
        perform_login_action($who, $login_options, ["to confirm that you", "para confirmar que deseja"]);
    }

    if ($login_action eq "abandon" || $login_action eq "suspend" || $login_action eq "wrongpassword") {
        verbose("Done creating pledge ". $page1->{'ref'} . " ($login_action)");
        return;
    }

    $wth->browser_check_contents(qr/(Thank you for creating your pledge|Obrigado por criar sua promessa)/);
    verbose("Done creating pledge ". $page1->{'ref'} . " ($login_action)");
    return;
}

# sign_pledge WHO SHOW_SIGNATURE LOGIN_ACTION EXPECTED [PARAMS]
# Adds a signer to a pledge, assumes browser is already pointing to pledge
# signup page.  Adds person number WHO.  SHOW_SIGNATURE is 1 for their
# signature to be public, undef for private.  LOGIN_ACTION is passed
# on to perform_login_action.  EXPECTED indicates what result is expected:
#   'signed' - a successful signing (default)
#   'reached' - a successful signing, which caused pledge to reach its target
#   'finished' - failed signing, because pledge has finished (closed, expired)
#   'already' - failed signing, because you've already signed.
#   'ownpledge' - failed signing, because you're the pledge creator.
# PARAMS contains more parameters.
#   'byarea' - if present and true, then this is a byarea (geocascading) type pledge
#              being signed
#   'byarea_near' - selection to make in the "Which local pledge would you like to join?" form
sub sign_pledge {
    my ($who, $show_signature, $login_options, $expected, $params) = @_;
    my $login_action = $login_options->{'action'};

    # Sign up now, unless we are resuming abandoned signup
    if ($login_action ne "resume") {
        if ($params->{byarea}) {
            $wth->browser_check_contents("Sign up where you live");
        } else { 
            $wth->browser_check_contents("Sign up now");
        }
        my $fields = { name => name_n($who), email => email_n($who), showname => $show_signature };
        if ($params->{byarea}) {
            $wth->browser_check_contents("Your town:");
            # This gives warnings about read only when it is a hidden field,
            # need to do something neater to know if the pledge requires it or not.
            #$fields->{'country'} = location_n($who)->{'country'};
            $fields->{'place'} = location_n($who)->{'place'};
        }
        $wth->browser_submit_form(form_name => 'pledge',
            fields => $fields,
            button => 'submit') or die "Failed to submit signing form";

        # Choose specific location
        if ($params->{byarea} && location_n($who)->{'gaze_place'}) {
            $wth->browser_check_contents("Now please select one of the possible places");
            $fields->{gaze_place} = location_n($who)->{'gaze_place'};
            $wth->browser_submit_form(form_name => 'pledge',
                fields => $fields,
                button => 'submit') or die "Failed to submit signing form, place selection stage";
        }

        # And choose from list of locations which already have signers
        if ($params->{byarea_near}) {
            $wth->browser_check_contents("Which local pledge would you like to join?");
            my $fields = undef;
            if ($params->{byarea_near} ne 'default') {
                $fields = { byarea_location_id => $params->{byarea_near} };
            }
            $wth->browser_submit_form(form_name => 'pledge',
                fields => $fields,
                button => 'submit') or die "Failed to submit confirm location form, place selection stage";
        }
    }

    # Perform appropriate login action
    perform_login_action($who, $login_options, ["to confirm your signature"]);
    return if ($login_action eq "abandon" || $login_action eq "suspend" || $login_action eq "wrongpassword");

    # Look for appropriate result in web page
    if ($expected eq 'signed' || $expected eq 'reached') {
        $wth->browser_check_contents("Thanks for signing");
        if ($expected eq 'reached') {
            $wth->browser_check_contents("Your signature has made this pledge reach its target!");
        }
    } elsif ($expected eq 'finished') {
        $wth->browser_check_contents("That pledge has already finished");
    } elsif ($expected eq 'already') {
        $wth->browser_check_contents("already signed this pledge");
    } elsif ($expected eq 'ownpledge') {
        $wth->browser_check_contents("cannot sign your own");
    } else {
        die "Unknown flag '$expected'";
    }
}

# find_and_sign_pledge FIND_TYPE FIND_STRING WHO SHOW_SIGNATURE LOGIN_ACTION EXPECTED PARAMS
# Finds a public pledge depending on FIND_TYPE
#   'frontpage' - find listing via string FIND_STRING on front page
#   'list_open' - find listing via string FIND_STRING on all pledges page
#   'list_succeeded_open' - find listing via string FIND_STRING on all pledges page
#   'ref' - go directly to ref which is in FIND_STRING
# se text FIND_STRING.  Then calls sign_pledge.
sub find_and_sign_pledge {
    my ($find_type, $find_string, $who, $show_signature, $login_options, $expected, $params) = @_;
    my $login_action = $login_options->{'action'};
    die "Find type '$find_type' not known" if $find_type ne 'frontpage' && $find_type ne 'list_open' && $find_type ne 'list_succeeded_open' && $find_type ne 'ref';

    if ($login_action ne "resume") {
        if ($find_type eq 'ref') {
            $wth->browser_get($locale_url . "/" . $find_string);
        } else {
            $wth->browser_get($locale_url);
            if ($find_type eq 'list_open' || $find_type eq 'list_succeeded_open') {
                $wth->browser_follow_link(text_regex => qr/All Pledges/) or die "Failed to find 'All Pledges' link";
            }
            if ($find_type eq 'list_succeeded_open') {
                $wth->browser_follow_link(text_regex => qr/Successful open/) or die "Failed to find 'Successful open' link";
            }
            $wth->browser_follow_link(text_regex => qr/$find_string/) or die "Failed to find '$find_string' link";
        }
    }
    sign_pledge($who, $show_signature, $login_options, $expected, $params);
}

# Change the date that all parts of PledgeBank think is today.  Call with no
# parameters to reset it to the actual today.
sub set_pb_date {
    my $new_date = shift;
    if (defined($new_date)) {
        dbh()->do('delete from debugdate');
        dbh()->do('insert into debugdate (override_today) values (?)', {}, $new_date);
    } else {
        dbh()->do('delete from debugdate');
    }
    dbh()->commit();
}

# set_pledge_prominence REF PROMINENCE
# Change pledge REF to have prominence PROMINENCE
sub set_pledge_prominence {
    my ($ref, $prominence) = @_;
    dbh()->do('update pledges set prominence = ? where ref = ?', {}, $prominence, $ref);
    dbh()->commit();
}

# Call frequentupdate cron job
sub call_frequentupdate {
    $wth->multi_spawn($multispawn, "php frequentupdate " . ($verbose > 1 ? qw(--verbose) : ''), $verbose);
}

# Call sendalerts cron job
sub call_send_alerts {
    $wth->multi_spawn($multispawn, "php send-comment-alerts " . ($verbose > 1 ? qw(--verbose) : ''), $verbose);
    $wth->multi_spawn($multispawn, "php send-local-alerts " . ($verbose > 1 ? qw(--verbose) : ''), $verbose);
}

# Call find-pledge-connections cron job
sub call_findpledgeconnections {
    $wth->multi_spawn($multispawn, "./find-pledge-connections " . ($verbose > 1 ? qw(--verbose) : ''), $verbose);
}

# Run pbsmsd once
sub call_smsd {
    $wth->multi_spawn($multispawn, "./pbsmsd --once " . ($verbose > 1 ? qw(--debug) : ''), $verbose);
}

#############################################################################
sub do_unconfirmed_pledge {
    comment("Make the pledge");
    set_pb_date('1980-12-01');
    my $pledge_action_unconfirmed = 'not confirm this pledge';
    my $pledge_ref = "neverconfirm";
    create_pledge(0, 
        { title => $pledge_action_unconfirmed, 
            target => '50', type => 'stubborn people', signup => 'will refuse ever to sign it',
            date => 'next thursday', ref => $pledge_ref,
        },
        { country => 'Global' },
        { },
        { action => "abandon" }
    );

    comment("Check not on front page");
    $wth->browser_get($locale_url);
    $wth->browser_check_no_contents($pledge_action_unconfirmed);
}

#############################################################################
sub do_public_succeeding_pledge {
    comment("Change to US URL (link on /where not there yet, as no pledges)");
    lock_locale_url("http://us.$stub_url");
    $wth->browser_follow_link(text_regex => qr/Pledge/);

    comment("Sign up to local alert");
    $wth->browser_get($locale_url);
    $wth->browser_submit_form(form_name => 'localalert',
        fields => {
            email => email_n(11),  # use same signer as one below, to test conversion from 
                                   # nameless to named person in the person table.
            country => 'US',
            place => 'Reno',
        },
        button => 'submit');
    # More recent Gaze has different coordinate for Reno
    my $reno_field = '39.52972|-119.81278|Reno, Nevada';
    my $sparks_field = '39.535|-119.75167|Sparks, Nevada';
    my $content = $wth->browser_content();
    if ($content !~ m/\Q$reno_field/) {
        $reno_field = '39.5296329|-119.8138027|Reno, Nevada';
        $sparks_field = '39.5349112|-119.7526886|Sparks, Nevada';
    }
    $wth->browser_submit_form(form_name => 'pledge',
        fields => {
            gaze_place => $reno_field},
        button => 'submit');
    perform_login_action(11, {action=>"viaemail"}, ["emailed whenever a new"]);
    $wth->browser_check_contents("Thanks for subscribing!");
    $wth->browser_follow_link(text_regex => qr/this isn't you/);

    comment("Make the pledge");
    set_pb_date('1982-02-01');
    my $pledge_action_completion = 'make sure a pledge can be completed';
    my $pledge_ref = "automatedtest";
    create_pledge(0, 
        { title => $pledge_action_completion, 
            target => '3', type => 'automated lines of code', signup => 'do the same',
            date => '10th February', ref => $pledge_ref,
            detail => "This pledge has just one purpose.  And that is as part of an automated test suite which ensures PledgeBank always works.  It is really quite good, in our humblest opinion.  It would save the planet, if only it was more comprehensive, and updated in a timely manner.\n\nAnd this bit is the start of a brand new shiny paragraph.  It should be formatted below on posters in the correct manner.",
            identity => 'lover of PledgeBank' },
        { country => 'US', 'local' => 1, place => 'Sparks', gaze_place => $sparks_field},
        { },
        { action => "viaemail" }
    );
    comment("Check no local alert yet (pledge has no signers, so will be backpage)");
    call_send_alerts();
    $wth->email_check_none_left();

    comment("Add a comment, which will subscribe user 90 to comment alerts");
    # (TODO: put these three comment posts in a local function)
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents("lover of PledgeBank");
    $wth->browser_submit_form(form_name => 'commentform',
        fields => {
            author_name => name_n(90), author_email => email_n(90),
            author_website => "http://www.flourish.org",
            text => "Hey! This is a great pledge.  We're sure to be able to know if the code is broken with this one.  Do I have to be a computer to take part?"
        },
        button => 'preview');
    $wth->browser_check_contents("Hey! This is a great pledge.");
    $wth->browser_submit_form(form_name => 'commentform', fields => {}, button => 'submit');
    perform_login_action(90, {action=>"viaemail"}, ["comment will then be displayed"]);
    $wth->browser_check_contents("Your comment has now been posted");
    $wth->browser_follow_link(text_regex => qr/to the pledge comments/);
    $wth->browser_check_contents("Do I have to be a computer to take part?");
    call_send_alerts();
    $wth->email_get_containing('%To: "'.name_n(0).'" <'.email_n(0). '>%This is a great pledge%');
    comment("Add another comment so its email goes");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_submit_form(form_name => 'commentform',
        fields => {
            author_name => name_n(91), author_email => email_n(91),
            text => "This is a second comment, which should be sent by email to first commenter.  It cost £0 to make and was mainly done in a café.  There are some unicode characters unwittily placed within."
        },
        button => 'preview');
    $wth->browser_check_contents("This is a second comment");
    $wth->browser_submit_form(form_name => 'commentform', fields => {}, button => 'submit');
    perform_login_action(91, {action=>"viaemail"}, ["comment will then be displayed"]);
    $wth->browser_check_contents("Your comment has now been posted");
    $wth->browser_follow_link(text_regex => qr/to the pledge comments/);
    $wth->browser_check_contents("It cost £0 to make and was mainly done in a café");
    call_send_alerts();
    $wth->email_get_containing(
        '%Content-Type: text/plain; charset%iso-8859-1%' .
        '%To: "'.name_n(0).'" <'.email_n(0).  
        encode("iso-8859-1", decode("UTF-8", '%It cost £0 to make and was%mainly done in a%café%')));
    my $comment_alert_email = $wth->email_get_containing('%To: "'.name_n(90).'" <'.email_n(90).  
        encode("iso-8859-1", decode("UTF-8", '%It cost £0 to make and was%mainly done in a%café%')));
    $wth->email_check_none_left();
    die $comment_alert_email . "\n\nUnsubscribe link not found" if ($comment_alert_email !~ m#\b(http://.*/L/.*)\b#m);
    my $comment_unsubscribe_url = $1;
    $wth->browser_get($comment_unsubscribe_url);
    $wth->browser_check_contents("You won't receive more email");
    comment("Send third comment, check don't get alert");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_submit_form(form_name => 'commentform',
        fields => {
            author_name => name_n(91), author_email => email_n(91),
            text => "This is a third comment, which should not be sent by email to first commenter."
        },
        button => 'preview');
    $wth->browser_check_contents("This is a third comment");
    $wth->browser_submit_form(form_name => 'commentform', fields => {}, button => 'submit');
    perform_login_action(91, {action=>"viaemail"}, ["comment will then be displayed"]);
    $wth->browser_check_contents("Your comment has now been posted");
    $wth->browser_follow_link(text_regex => qr/to the pledge comments/);
    $wth->browser_check_contents("This is a third comment");
    call_send_alerts();
    $wth->email_get_containing('%To: "' . name_n(0) . '" <' . email_n(0) . '>%This is a third comment%');
    $wth->email_check_none_left();

    #comment("Mail pledge to friends");
    #$wth->browser_get($locale_url . "/$pledge_ref/email");
    #$wth->browser_check_contents("Email this pledge");
    #my $friend_message = "Hello you!";
    #$wth->browser_submit_form(form_name => 'pledge',
    #    fields => { 
    #        fromname => name_n(0), fromat => email_n(0), 
    #        frommessage => $friend_message,
    #        e1 =>  email_n(10),
    #        e2 =>  email_n(11),
    #        e3 =>  email_n(12),
    #        # deliberate gap
    #        e5 =>  email_n(13),
    #        },
    #    button => 'submit') or die "Failed to submit friend emailing form";
    #$wth->browser_check_contents("Thanks very much for spreading the word");
    #for (my $i = 10; $i <= 13; $i++) {
    #    my $content = $wth->email_get_containing('%To: '.email_n($i).'%thought%you%would%be%');
    #    $content =~ m#$locale_url_redirected/$pledge_ref# or die "Failed to find pledge link $locale_url_redirected in friend email";
    #    $content =~ m#$friend_message# or die "Failed to find message in friend email";
    #    $content =~ m## or die "Failed to find pledge link in friend email";
    #}

    comment("Sign it twice");
    find_and_sign_pledge("ref", $pledge_ref, 1, 1, {action=>"viaemail"}, "signed");
    call_frequentupdate(); # update prominence cache
    find_and_sign_pledge("list_open", $pledge_action_completion, 2, undef, {action=>"viaemail"}, "signed");

    comment("Now we have a signer, will get local alert");
    call_send_alerts();
    $wth->email_get_containing('%To: ' . email_n(11) . '%new pledge%'.$pledge_action_completion.'%');

    comment("Check can't get to announce page as a mere signer");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_follow_link(text_regex => qr/message to signers/);
    $wth->browser_check_contents("must be the pledge creator");
    $wth->browser_follow_link(text_regex => qr/log out/);
    comment("Send mid-pledge announcement, to encourage signers to do more to help");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_follow_link(text_regex => qr/message to signers/);
    perform_login_action(0, {action=>"viaemail", giveemail=>1}, ["send a message to everyone"]);
    $wth->browser_check_contents("Send announcement");
    $wth->browser_check_no_contents("successful");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "Hey, please mail all your friends and get them to sign the pledge.  It's sure to work then! Love and kisses, " . name_n(0),
                },
        button => 'submit') or die "Failed to submit email success form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge");
    call_frequentupdate();
    comment("Check all announcements arrived");
    $wth->email_get_containing( '%To: "' . name_n(0) . '" <' . email_n(0) . '>%From: '.$name_admin.  '%[ This is a copy of the message%Love and kisses%');
    $content = $wth->email_get_containing( '%To: "' . name_n(1) . '" <' . email_n(1) . '>%From: "' . name_n(0) . '"%Love and kisses%');
    $wth->email_get_containing( '%To: "' . name_n(2) . '" <' . email_n(2) . '>%From: "' . name_n(0) . '"%Love and kisses%');
    die "Unsubscribe email link not found\n" if ($content !~ m#^\s*(http://.*/L/.*$)#m);
    my $unsub_link = $1;

    comment("Unsubscribe one signer from announce messages");
    display_url("Unsubscribe from announcements URL", $unsub_link);
    $wth->browser_get($unsub_link);
    $wth->browser_check_contents("won't receive any more emails");

    call_frequentupdate();
    $wth->email_check_none_left();

    comment("Try to sign the pledge a second time as the same person");
    find_and_sign_pledge("list_open", $pledge_action_completion, 2, 1, {action=>"viaemail"}, "already");
    comment("... and as the creator, which also isn't allowed");
    find_and_sign_pledge("list_open", $pledge_action_completion, 0, 1, {action=>"viaemail"}, "ownpledge");

    comment("Sign it once more, this tipping balance");
    find_and_sign_pledge("list_open", $pledge_action_completion, 3, 1, {action=>"viaemail"}, "reached");

    comment("Partially sign pledge before completion state change...");
    find_and_sign_pledge("list_succeeded_open", $pledge_action_completion, 4, 1, {action=>"suspend"}, "signed");

    comment("Provoke state change, and check pledge has completed");
#print "Press RETURN to continue"; readline(*STDIN);
    call_frequentupdate();
#print "Press RETURN to continue"; readline(*STDIN);
    $wth->browser_get($locale_url);
    $wth->browser_check_contents("Target met, pledge still open for 9 days");
    $wth->browser_follow_link(text_regex => qr/$pledge_ref/);
    $wth->browser_check_contents("This pledge has been successful!");
    $wth->browser_check_contents("1 person who did not want to give their name");
    comment("Check for late mid-pledge announce message");
    $wth->email_get_containing( '%To: "' . name_n(3) . '" <' . email_n(3) . '>%From: "' . name_n(0) . '"%You signed this pledge after this message%Love and kisses%');
    comment("Check success email there");
    my $announce_link = undef;
    for (my $i = 0; $i <= 3; ++$i) {
        if ($i == 2) {
            $wth->browser_check_no_contents(name_n($i));
        } else {
            $wth->browser_check_contents(name_n($i));
        }
        # Check got success emails
        my $success_email = $wth->email_get_containing('%To: "' . name_n($i) . '" <' . email_n($i) . '>%We are pleased to tell you%');
    }

    comment("Wait for reminder message");
    set_pb_date('1982-02-04');
    call_frequentupdate();
    my $success_email = $wth->email_get_containing('%To: "' . name_n(0) . '" <' . email_n(0) . '>%Your pledge reached its target three days ago%');
    die "Success email link not found\n" if ($success_email !~ m#^\s*(http://.*$)#m);
    $announce_link = $1;

    comment("Send success message using announcements page, from creator to signers.");
    die "Failed to find announcements link" if !$announce_link;
    display_url("Announce URL", $announce_link);
    $wth->browser_get($announce_link);
    $wth->browser_check_contents("Send announcement");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "By merely partaking you have fulfilled your part in the pledge.  You rule!  This is my favourite pledge in the world and it has a lot more text these days.  Yes, it goes on and on and on for many many lines, and hopefully even word wraps in the email that it generates later.\n\nHopefully.\n\nYours sincerely, \n\n" . name_n(0),
                           # no message_sms, as US pledges don't support SMS
                           },
        button => 'submit') or die "Failed to submit email success form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge");
    call_frequentupdate();
    comment("Check all success messages arrived");
    for (my $i = 2; $i <= 3; ++$i) {
        my $success_email = $wth->email_get_containing( 
            '%To: "' . name_n($i) . '" <' . email_n($i) . '>%From: "' . name_n(0) . '"%By merely partaking%');
    }
    $success_email = $wth->email_get_containing( 
        '%To: "' . name_n(0) . '" <' . email_n(0) . '>%From: ' . $name_admin . '%[ This is a copy of the message%By merely partaking%');
    comment("Make sure no extra emails - in particular that late partial-signer hasn't got message");
    call_frequentupdate();
    # TODO: Make this next line check that we only have the (unconfirmed) signup
    # message in the mail queue, and no other messages.
    # $wth->email_check_none_left();

    comment("... now complete partial signature (a day later, so it contains new extra header)");
    set_pb_date('1982-02-05');
    find_and_sign_pledge("list_succeeded_open", $pledge_action_completion, 4, undef, {action=>"resume"}, "signed");
    comment("Check the late signer (number 4) also recieves the success announcement message.");
    comment("They won't get the earlier success announcement message, instead a URL...");
    call_frequentupdate();
    my $late_success_announce_email = $wth->email_get_containing( '%To: "' . name_n(4) . '" <' . email_n(4) . '>%You signed this pledge after this message was%By merely partaking%');
    die $late_success_announce_email . "\n\nRead more link not found" if ($late_success_announce_email !~ m#\b(http://.*/L/.*)\b#m);
    my $announce_archive_link = $1;
    comment("... follow it to get other messages");
    $wth->browser_get($announce_archive_link);
    $wth->browser_check_contents("Love and kisses");
    $wth->browser_check_contents("By merely partaking");

    # (commented out as not very important, but hard to auto test without copying
    # lots of code from last find_and_sign_pledge for the last one)
    ## Stash test for expired tokens
    #find_and_sign_pledge($pledge_action_completion, 5, 1, {action=>"suspend"}, "signed");
    #set_pb_date('1982-02-');
    ## ... this as side effect provokes deletion of 7 day old stashes
    #find_and_sign_pledge($pledge_action_completion, 6, 1, {action=>"abandon"}, "signed");
    ## ... and check what error we get
    #find_and_sign_pledge($pledge_action_completion, 5, 1, {aciton=>"resume:nopw"}, "signed");

    comment("Check what happens when the pledge expires");
    set_pb_date('1982-02-11');
    comment("Pledge should say closed and successful");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents("This pledge has now closed");
    $wth->browser_check_no_contents("Sign up now");
    $wth->browser_check_contents("it was successful!");
    $wth->browser_check_contents(name_n(4));
    $wth->browser_check_no_contents(name_n(5));

    $wth->email_check_none_left();

    comment("And now the survey emails...");
    set_pb_date('1982-03-01');
    call_frequentupdate();
    for (my $i = 1; $i <= 4; ++$i) {
        my $survey_email = $wth->email_get_containing( 
            '%To: "' . name_n($i) . '" <' . email_n($i) . '>%From: ' . $name_admin . '%If you HAVE done your pledge%');
        if ($i == 3) {
            die $survey_email . "\n\nSurvey link not found" if ($survey_email !~ m#\b(http://.*/L/.*)\b#m);
            my $survey_url = $1;
            $wth->browser_get($survey_url);
        }
    }
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents('value="pledge">' . name_n(3)); # there is a form round the new signer now, hence the value="pledge" bit, which is part of that form
    $wth->email_check_none_left();
    $wth->browser_follow_link(text_regex => qr/this isn't you/);

    comment("Change back to global URL");
    lock_locale_url($base_url);
}

#############################################################################
sub do_private_failing_pledge {
    my $pledge_action_private = 'check private pledges are private';
    my $pledge_ref = "privatepledge";
    my $private_pin = 'mypin';
    set_pb_date('1980-12-01');
    create_pledge(0, 
        { title => $pledge_action_private, 
            target => '10', date => 'Dec 31st', ref => $pledge_ref
        },
        { country => 'Global' },
        { visibility => 'pin', pin => $private_pin },
        { action => "viaemail" }
    );
    comment("Make frontpage, to be sure that doesn't make it show");
    set_pledge_prominence($pledge_ref, 'frontpage');

    comment("Go to private pledge page, and submit PIN to access it");
    my $goto_private_pledge_page = sub {
        $wth->browser_get("$locale_url/$pledge_ref");
        $wth->browser_check_contents("PIN Protected Pledge");
        $wth->browser_check_no_contents("Incorrect PIN!");
        $wth->browser_check_no_contents($pledge_action_private);
        $wth->browser_submit_form(form_name => 'pledge',
                fields => { pin => 'thisisnotthepin' },
                button => 'submitpin') or die "Failed to submit pin form";
        $wth->browser_check_contents("Incorrect PIN!");
        $wth->browser_check_no_contents($pledge_action_private);
        $wth->browser_submit_form(form_name => 'pledge',
                fields => { pin => $private_pin },
                button => 'submitpin') or die "Failed to submit pin form";
        $wth->browser_check_contents($pledge_action_private);
    };

    comment("Sign up to private pledge");
    &$goto_private_pledge_page();
    $wth->browser_check_no_contents("This pledge is now closed");
    sign_pledge(1, 1, {action=>"viaemail"}, "signed");
    &$goto_private_pledge_page();
    $wth->browser_check_contents("<i>1 person has signed up");

    comment("Try to sign up by SMS and fail, as it isn't allowed for private pledges");
    sms_fake_to_pledgebank(10, "pledge $pledge_ref");
    die "Didn't get OK for incoming SMS" if $wth->browser_content() ne "OK";
    call_smsd();
    my $sms_text = sms_get_containing(mobile_n(10), "%We can't find the pledge%", 0);
    verbose("Private sign attempt SMS text: $sms_text");
    comment("Try to sign up by SMS, with partial pledge identifier");
    sms_fake_to_pledgebank(10, "pledge rivatepl");
    die "Didn't get OK for incoming SMS" if $wth->browser_content() ne "OK";
    call_smsd();
    $sms_text = sms_get_containing(mobile_n(10), "%We can't find the pledge%", 0);
    verbose("Private sign attempt SMS text: $sms_text");
    comment("Wait for delivery notification, which would confirm signature");
    comment("(want to test that nothing happens, as nothing was signed)");
    set_pb_date('1980-12-02');
    call_smsd();  # delivery notification
    call_smsd();  # cull done with message
    comment("Check we don't get any information leaks");
    sms_check_none_left();
    comment("... in particular there are no new signups");
    &$goto_private_pledge_page();
    $wth->browser_check_contents("<i>1 person has signed up");
    $wth->browser_check_no_contents("This pledge is now closed");
 
    verbose("Checking no leaks of private pledge...");

    comment("Check pledge really is private on lists of pledges");
    comment("... index page, all pledges");
    $wth->browser_get($locale_url);
    $wth->browser_check_no_contents($pledge_ref);
    $wth->browser_check_no_contents($pledge_action_private);
    comment("... all pledges page");
    $wth->browser_get($locale_url . "/all");
    $wth->browser_check_no_contents($pledge_ref);
    $wth->browser_check_no_contents($pledge_action_private);
    comment("... RSS");
    $wth->browser_get($locale_url . "/rss");
    $wth->browser_check_no_contents($pledge_ref);
    $wth->browser_check_no_contents($pledge_action_private);

    comment("And all its pages require PIN to get to");
    comment("... email friends page");
    $wth->browser_get("$locale_url/$pledge_ref/email");
    $wth->browser_check_contents("PIN Protected Pledge");
    $wth->browser_check_no_contents($pledge_action_private);
    comment("... flyers index");
    $wth->browser_get("$locale_url/$pledge_ref/flyers");
    $wth->browser_check_contents("PIN Protected Pledge");
    $wth->browser_check_no_contents($pledge_action_private);
    comment("... individual poster");
    $wth->browser_get("$locale_url/flyers/${pledge_ref}_A4_flyers8.pdf");
    $wth->browser_check_contents("Correct PIN needed");

    comment("... sign page");
    $wth->browser_get("$locale_url/$pledge_ref/sign");
    $wth->browser_check_contents("Permission denied");
    $wth->browser_check_no_contents($pledge_action_private);

    #comment("Provoke chivvy too few signers email");
    #set_pb_date('1980-12-07');
    #call_frequentupdate();
    #$wth->email_get_containing('%Subject: You MUST publicise%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    #$wth->email_check_none_left();
    #comment("Provoke chivvy rate too low mail (we get it even though low signers count");
    #comment("as we are 'frontpage')");
    #set_pb_date('1980-12-10');
    #call_frequentupdate();
    #$wth->email_get_containing('%To: "' . name_n(0) . '" <' . email_n(0) . '>%rate of growth you\'ve had over the last week%');
    #$wth->email_check_none_left();

    comment("Test what happens upon expiry");
    comment("... partially sign the pledge");
    &$goto_private_pledge_page();
    sign_pledge(2, 1, {action=>"suspend"}, undef);
    comment("... then expire it");
    set_pb_date('1981-01-15');
    comment("... make sure completing the partial signing fails");
    sign_pledge(2, 1, {action=>"resume"}, "finished");
    comment("Check failure emails");
    call_frequentupdate();
    my $failure_email = $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n(0) . '" <' . email_n(0) . '>%'); # failed
    die "Failure email link not found\n" if ($failure_email !~ m#^\s*(http://.*$)#m);
    my $announce_link = $1;
    $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n(1) . '" <' . email_n(1) . '>%'); # failed

    comment("Make sure pledge says it is expired, and has no sign up box");
    &$goto_private_pledge_page();
    $wth->browser_check_contents("This pledge is now closed");
    $wth->browser_check_no_contents("Sign up now");

    comment("Send failure message using announcements page, from creators to signers");
    die "Failed to find announcements link" if !$announce_link;
    display_url("Announce URL", $announce_link);
    $wth->browser_get($announce_link);
    $wth->browser_check_contents("Send announcement");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "What a hassle.  It is so very irksome when a pledge doesn't work.  Please sign all my other pledges instead!\n\nYours sincerely, \n\n" . name_n(0),
                            message_sms => "U suk. " . (split(m/ /,name_n(0)))[0]. " x"},
        button => 'submit') or die "Failed to submit email failure form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge");
    call_frequentupdate();
    my $success_email = $wth->email_get_containing('%To: "' . name_n(0) . '" <' . email_n(0) . '>%From: ' . $name_admin . '%[ This is a copy of the message%What a hassle%');
    $success_email = $wth->email_get_containing('%To: "' . name_n(1) . '" <' . email_n(1) . '>%From: "' . name_n(0) . '"%What a hassle%');
    $wth->email_check_none_left();
    comment("Try to send second failure, which is not allowed");
    $wth->browser_get($locale_url . "/$pledge_ref/announce");
    $wth->browser_check_contents("You have already sent 1 failure announcement");

    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");
}

#############################################################################

sub do_chivvy_testing {
    my $pledge_action = 'check chivvy emails are sent appropriately';
    my $pledge_ref = "chivvytesting";
    set_pb_date('1985-12-01');
    create_pledge(0, 
        { title => $pledge_action, 
            target => '10', date => 'Dec 31st', ref => $pledge_ref
        },
        { country => 'Global' },
        { },
        { action => "viaemail" }
    );
    comment("Make frontpage, so that all chivvy emails are sent");
    set_pledge_prominence($pledge_ref, 'frontpage');

    comment("Provoke chivvy rate too low mails");
    set_pb_date('1985-12-04'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-05'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-06'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-07'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-08'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Helping to make your pledge succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-10'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-11'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge isn\'t growing fast enough%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-13'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-14'); call_frequentupdate();
    $wth->email_get_containing('%Subject: A suggestion to help your pledge succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-15'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-17'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge needs to grow faster to succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-19'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-20'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Need some help to get your pledge to grow faster%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-22'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-23'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your local politicians can help your pledge to grow%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-24'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-26'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Bribe your friends to help spread your pledge%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-29'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1986-01-01'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n(0) . '" <' . email_n(0) . '>%'); # failed
    $wth->email_check_none_left();

    comment("Now check no more than one a day, even if frequentupdate bombs and isn't run for two weeks");
    $pledge_action = 'check chivvy emails are sent appropriately again';
    $pledge_ref = "chivvytesting2";
    set_pb_date('1985-12-01');
    create_pledge(0, 
        { title => $pledge_action, 
            target => '10', date => 'Dec 31st', ref => $pledge_ref
        },
        { country => 'Global' },
        { },
        { action => "loggedin" }
    );
    set_pledge_prominence($pledge_ref, 'frontpage');
    set_pb_date('1985-12-15'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Helping to make your pledge succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-17'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-18'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge isn\'t growing fast enough%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-19'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-21'); call_frequentupdate();
    $wth->email_get_containing('%Subject: A suggestion to help your pledge succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-23'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-24'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge needs to grow faster to succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-26'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-27'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Need some help to get your pledge to grow faster%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-29'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1986-01-01'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n(0) . '" <' . email_n(0) . '>%'); # failed
    $wth->email_check_none_left();

    comment("Now check nothing sent if there has been some signups!");
    $pledge_action = 'check chivvy emails are sent appropriately yet again';
    $pledge_ref = "chivvytesting3";
    set_pb_date('1985-12-01');
    create_pledge(0, 
        { title => $pledge_action, 
            target => '10', date => 'Dec 31st', ref => $pledge_ref
        },
        { country => 'Global' },
        { },
        { action => "loggedin" } # loggedin
    );
    set_pledge_prominence($pledge_ref, 'frontpage');
    
    set_pb_date('1985-12-03');
    find_and_sign_pledge('ref', $pledge_ref, 101, 1, {action=>'viaemail'}, 'signed');
    find_and_sign_pledge('ref', $pledge_ref, 102, 1, {action=>'viaemail'}, 'signed');
    find_and_sign_pledge('ref', $pledge_ref, 103, 1, {action=>'viaemail'}, 'signed');
    set_pb_date('1985-12-09'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-10'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-12'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Helping to make your pledge succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-13'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-15'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge isn\'t growing fast enough%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-17'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-18'); call_frequentupdate();
    $wth->email_get_containing('%Subject: A suggestion to help your pledge succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-20'); call_frequentupdate();
    $wth->email_check_none_left();
    find_and_sign_pledge('ref', $pledge_ref, 104, 1, {action=>'viaemail'}, 'signed');
    find_and_sign_pledge('ref', $pledge_ref, 105, 1, {action=>'viaemail'}, 'signed');
    find_and_sign_pledge('ref', $pledge_ref, 106, 1, {action=>'viaemail'}, 'signed');
    set_pb_date('1985-12-21'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-23'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-24'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge needs to grow faster to succeed%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-26'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-27'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Need some help to get your pledge to grow faster%To: "' . name_n(0) . '" <' . email_n(0) . '>%');
    $wth->email_check_none_left();
    set_pb_date('1985-12-28'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1985-12-31'); call_frequentupdate();
    $wth->email_check_none_left();
    set_pb_date('1986-01-01'); call_frequentupdate();
    $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n(0) . '" <' . email_n(0) . '>%'); # failed
    for (my $i=101; $i<=106; $i++) {
        $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n($i) . '" <' . email_n($i) . '>%'); # failed
    }
    $wth->email_check_none_left();
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
}

#############################################################################

sub sms_setup {
    # Create table for outgoing SMS messages to be stored in (by pledgebank/web/test-sms.cgi)
    dbh()->do("create table testharness_sms (
      id serial not null primary key,
      mobile text not null,
      message text not null,
      premium boolean not null)");
    dbh()->commit();
}

# sms_fake_to_pledgebank WHO MESSAGE
# Calls incoming SMS interface as if message MESSAGE had just been sent from
# the mobile phone WHO to number 60022.
our $sms_sequence = 1000;
sub sms_fake_to_pledgebank {
    my ($who, $message) = @_;
    $sms_sequence++;
    $wth->browser_post($sms_in_url, { 
            intSequence => $sms_sequence, # go up by one each time
            intTransactionID => 1, # ?
            intTime => '19820301070000', # date/time
            intDestination => '60022', # pledgebank number
            intOriginatingNumber => mobile_n($who), # mobile from #
            intDeliverer => 1, # ?
            strData => $message
    });
}

# email_get_containing MOBILE MESSAGE PREMIUM
# Returns the email containing the given STRING as an SQL expression.  i.e. Use %
# for wildcard.  It is an error if no matching mails are found within a few
# seconds, or there is more than one match.
sub sms_get_containing($$$) {
    my ($mobile, $message_like, $premium) = @_;

    # Find matching message
    my $mails = dbh()->selectall_arrayref("select id, message from testharness_sms
        where mobile = ? and message like ? and premium = ?", {}, $mobile, $message_like, $premium ? 't' : 'f');
    my $got = scalar @$mails;
    die "SMS for $mobile with '$message_like' premium $premium not found" if ($got == 0);
    die "Too many SMS found for $mobile with '$message_like' premium $premium" if ($got > 1);

    # Get content
    my ($id, $message) = @{$mails->[0]};

    # TODO: Save to logging smsbox

    # Delete from incoming queue
    dbh()->do("delete from testharness_sms where id = ?", {}, $id);
    dbh()->commit();
    return $message;
}

# sms_check_none_left
# Throws an error if there are any queued SMS messages, incoming or outgoing,
# left.
sub sms_check_none_left {
    my $received_left = dbh()->selectrow_array("select count(*) from testharness_sms");
    die "$received_left unexpected SMS received by person" if $received_left > 0;
    my $outgoing_left = dbh()->selectrow_array("select count(*) from outgoingsms");
    die "$outgoing_left unexpected outgoing SMS left" if $outgoing_left > 0;
    my $incoming_left = dbh()->selectrow_array("select count(*) from incomingsms");
    die "$incoming_left unexpected incoming SMS left" if $incoming_left > 0;
}

sub do_sms_pledge {
    comment("Make the pledge");
    set_pb_date('1982-03-01');
    my $pledge_action_sms = 'pretend to be an SMS aggregating service';
    my $pledge_ref = "smsspoof";
    create_pledge(100, 
        { title => $pledge_action_sms, 
            target => '2', type => 'spoofed texting facilities', signup => 'do the same',
            date => '10th March', ref => $pledge_ref,
            detail => "Not sure what SMS is?  Find out here! www.wikipedia.org/wiki/Short_message_service Fab.  For more information, please visit www.ewb-uk.org/placements/2005/suriname. For more information about Engineers Without Borders UK, please visit www.ewb-uk.org, or email enquiries\@ewb-uk.org. For information regarding the project in Suriname, email suriname2005\@ewb-uk.org." },
        { country => 'Global' },
        { },
        { action => "viaemail" }
    );

    comment("Three basic SMS signups");
    for (my $who = 101; $who <= 103; ++$who) {
        sms_fake_to_pledgebank($who, "pledge smsspoof");
        $wth->browser_check_contents(qr/^OK$/);
        call_smsd();
        # All first person, as sign up simultaneously
        my $conversion_sms = sms_get_containing(mobile_n($who), "%You are the 1st person to pledge%", 0);
        verbose("SMS text: $conversion_sms");
    }
    comment("Check not got signup yet");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents("<i>0 people have signed up");
    comment("Wait for delivery notification, which confirms signature");
    set_pb_date('1982-03-02');
    call_smsd();  # delivery notification
    call_smsd();  # cull done with message
    comment("Check signup now appears");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents("<i>3 people have signed up");

    comment("SMS signup with conversion");
    sms_fake_to_pledgebank(105, "pledge smsspoof");
    die "Didn't get OK for incoming SMS" if $wth->browser_content() ne "OK";
    call_smsd();
    my $conversion_sms = sms_get_containing(mobile_n(105), "%You are the 4th person to pledge%", 0);
    verbose("SMS text: $conversion_sms");
    my $base_url_without_http = $base_url; $base_url_without_http =~ s#http://##;
    die "Conversion link not found in SMS '$conversion_sms'\n" if ($conversion_sms !~ m#\b($base_url_without_http[^\s]*)\b#m);
    my $conversion_url = "http://".$1;
    verbose("SMS conversion URL $conversion_url");
    comment("Go to conversion link and fill in conversion form");
    $wth->browser_get($conversion_url);
    $wth->browser_check_contents("rather than sending an SMS");
    $wth->browser_submit_form(form_name => 'pledge',
        fields => { 
            phone => mobile_n(105), name => name_n(105), email => email_n(105), showname => 1
            },
        button => 'submit') or die "Failed to submit SMS conversion form";
    perform_login_action(105, {action=>"viaemail"}, ["to confirm your signature"]);
    $wth->browser_check_contents("Thanks for signing up");
    comment("Check signup now appears under name");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents(name_n(105));
    comment("Wait for delivery notification, which shouldn't harm anything");
    set_pb_date('1982-03-03');
    call_smsd();  # delivery notification
    call_smsd();  # cull done with message
    comment("Check signup still appears");
    $wth->browser_get($locale_url . "/$pledge_ref");
    $wth->browser_check_contents(name_n(105));

    comment("Let pledge succeed");
    set_pb_date('1982-03-09');
    call_frequentupdate();
    my $success_email = $wth->email_get_containing( '%To: "' . name_n(100) . '" <' . email_n(100) . '>%We are pleased to tell you%');
    die "Announce link not found\n" if ($success_email !~ m#^\s*(http://.*$)#m);
    my $announce_link = $1;
    display_url("Announce URL", $announce_link);
    $wth->email_get_containing( '%To: "' . name_n(105) . '" <' . email_n(105) . '>%We are pleased to tell you%');
    comment("Send announce message");
    $wth->browser_get($announce_link);
    $wth->browser_check_contents("Send announcement");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "It all worked, hurrah! Things are good when they work, especially messages to people when a pledge has been completed. Blah blah blah blah blah blah. " . name_n(100),
                            message_sms => "Yippee! All the £ are yours!" . (split(m/ /,name_n(100)))[0]. " x"},
        button => 'submit') or die "Failed to submit SMS/email announce form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge");
    call_frequentupdate();
    call_smsd();
    comment("Check all the success texts arrived");
    $wth->email_get_containing( '%To: "' . name_n(100) . '" <' . email_n(100) . '>%[ This is a copy of the message%hurrah!%');
    $wth->email_get_containing( '%To: "' . name_n(105) . '" <' . email_n(105) . '>%hurrah!%');
    comment("... the input unicode pound sign should come out as HTML entity");
    for (my $who = 101; $who <= 103; ++$who) {
        my $sms_text = sms_get_containing(mobile_n($who), '%Yippee! All the &#x00a3; are yours!%', 0);
        verbose("SMS text: $sms_text");
    }

    set_pb_date('1982-03-10');
    call_smsd();  # delivery notification
    call_smsd();  # cull done with message

    comment("Provoke survey email");
    set_pb_date('1982-05-01');
    call_frequentupdate();
    my $survey_email = $wth->email_get_containing( 
        '%To: "' . name_n(105) . '" <' . email_n(105) . '>%From: ' . $name_admin . '%If you HAVE done your pledge%');

    comment("Check nothing left");
    sms_check_none_left();
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->email_check_none_left();
}

#############################################################################
sub do_login_tests {
    comment("Sign up to local alert");
    $wth->browser_get($locale_url);
    $wth->browser_submit_form(form_name => 'localalert',
        fields => {
            email => email_n(250),  
            country => 'GB'
        },
        button => 'submit');
    $wth->browser_submit_form(form_name => 'pledge',
        fields => {
            postcode => 'SN81rF', # Red Lion pub, Avebury
        },
        button => 'submit');
    perform_login_action(250, {action=>"viaemail"}, ["emailed whenever a new"]);
    $wth->browser_check_contents("Thanks for subscribing!");
    $wth->browser_follow_link(text_regex => qr/this isn't you/);

    # Make the pledge
    set_pb_date('1983-06-06');
    my $pledge_action_login_1 = 'make multiple pledges using the magic of login';
    my $pledge_action_login_2 = 'be wary about flooding PledgeBank with meaningless pledges';
    my $pledge_action_login_3 = 'carry on making even more pledges forever and ever and ever';
    my $pledge_action_login_4 = 'go on and on and on';
    my $pledge_ref_1 = "login1";
    my $pledge_ref_2 = "login2";
    my $pledge_ref_3 = "login3";
    my $pledge_ref_4 = "login4";

    comment("Make first pledge (and set password)");
    create_pledge(200, 
        { title => $pledge_action_login_1, 
            target => '3', type => 'automated lines of code', signup => 'do the same',
            date => '1987-01-01', ref => $pledge_ref_1 },
        { country => 'GB', 'local' => 1, postcode => '  SN8  1AA' }, # Waitrose, Marlborough
        { },
        { action => "viaemail", setpassword => 1 }
    );
    comment("Checked logged in, and then logout");
    $wth->browser_get($locale_url);
    $wth->browser_check_contents("Hello, " . name_n(200));
    $wth->browser_check_no_contents("Login");
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");
    $wth->browser_check_no_contents(name_n(200));

    comment("Second pledge, this will set password");
    create_pledge(200, 
        { title => $pledge_action_login_2, 
            target => '3', type => 'automated lines of code', signup => 'do the same',
            date => '1987-01-01', ref => $pledge_ref_2 },
        { country => 'GB', 'local' => 1, place => 'marlborough' },
        { },
        { action => "password" }
    );
    comment("Log out again");
    $wth->browser_get($locale_url);
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");

    comment("Third pledge, use password to log in");
    create_pledge(200, 
        { title => $pledge_action_login_3, 
            target => '3', type => 'automated lines of code', signup => 'do the same',
            date => '1987-01-01', ref => $pledge_ref_3 },
        { country => 'Global' },
        { },
        { action => "password" }
    );
    comment("Log out again");
    $wth->browser_get($locale_url);
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");

    comment("Log in explicitly with 'login' link");
    $wth->browser_get($locale_url);
    comment("... first update pledge connections (to test that a bit on my pledges page)");
    call_findpledgeconnections();
    $wth->browser_follow_link(text_regex => qr/Login/);
    perform_login_action(200, {action=>"password", giveemail=>1}, undef);
    comment("... check gone to My Pledges page correctly");
    $wth->browser_check_contents("Open pledges you created");
    $wth->browser_check_contents($pledge_action_login_1);
    $wth->browser_check_contents($pledge_action_login_2);
    $wth->browser_check_contents($pledge_action_login_3);
    $wth->browser_check_no_contents($pledge_action_login_4);

    comment("Fourth pledge, expects already logged in");
    create_pledge(200, 
        { title => $pledge_action_login_4, 
            target => '3', type => 'automated lines of code', signup => 'do the same',
            date => '1987-01-01', ref => $pledge_ref_4 },
        { country => 'GB', 'local' => 0 },
        { },
        { action => "loggedin" }
    );
    
    comment("Check no alerts until we get one signer");
    call_send_alerts();
    $wth->email_check_none_left();

    comment("Sign all the pledges as another user");
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");
    find_and_sign_pledge("ref", $pledge_ref_1, 201, 1, {action=>"viaemail",setpassword=>1}, "signed");
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");
    comment("... takes a second go to get the set password form");
    find_and_sign_pledge("ref", $pledge_ref_2, 201, 1, {action=>"password"}, "signed");
    comment("Now we can sign as already logged in");
    find_and_sign_pledge("ref", $pledge_ref_3, 201, 1, {action=>"loggedin"}, "signed");
    comment("Logout again (in order to log in with password)");
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->browser_check_contents("now logged out");
    comment("... try with wrong password");
    find_and_sign_pledge("ref", $pledge_ref_4, 201, 1, {action=>"wrongpassword"}, "signed");
    comment("... then with the right one");
    find_and_sign_pledge("ref", $pledge_ref_4, 201, 1, {action=>"password"}, "signed");

    comment("Check local alert for two pledges at once works");
    call_frequentupdate(); # update prominence cache
    call_send_alerts();
    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->email_get_containing('%To: ' . email_n(250) . '%2 new pledge%login1%login2%');
    $wth->email_check_none_left();

}

#############################################################################
sub do_category_tests {
    set_pb_date('1984-01-01');
    lock_locale_url($base_url);

    comment("Make Portuguese language pledge");
    my $pledge_action_portuguese = 'learn to speak Portuguese';
    my $pledge_ref_portugese = "portuguese";
    $wth->browser_submit_form(with_fields => { lang => 'pt-br' });
    lock_locale_url();
    create_pledge(300, 
        { title => $pledge_action_portuguese, 
            target => '3', type => 'other monoglots', signup => 'do the same',
            date => '1988-12-31', ref => $pledge_ref_portugese },
        { country => 'PT', 'local' => 0 },
        { },
        { action => "viaemail", setpassword => 0 }
    );
    $wth->browser_get($locale_url_redirected);
    $wth->browser_submit_form(form_name => 'language',
        fields => { lang => 'en-gb' }) or die "Failed to change language";
    lock_locale_url();

    comment("Make English language pledge");
    my $pledge_action_english = 'stop English taking over as the world language';
    my $pledge_ref_english = "english";
    create_pledge(300, 
        { title => $pledge_action_english, 
            target => '3', type => 'other polyglots', signup => 'do the same',
            date => '1988-12-31', ref => $pledge_ref_english },
        { country => 'PT', 'local' => 0 }, 
        { },
        { action => "loggedin", setpassword => 0 }
    );

    comment("Set prominence");
    set_pledge_prominence($pledge_ref_english, 'frontpage');
    set_pledge_prominence($pledge_ref_portugese, 'frontpage');
    call_frequentupdate(); # update prominence cache

    comment("Change to Portugal pages");
    $wth->browser_get($locale_url_redirected . "/where"); # Not sure what country we're in...
    $wth->browser_follow_link(text => "Portugal");
    lock_locale_url();

    comment("Check pledges appear correctly");
    $wth->browser_get($locale_url_redirected);
    $wth->browser_check_contents("desde que 3 other monoglots");
    $wth->browser_check_contents("but only if 3 other polyglots");

    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    comment("Change to normal pages again");
    $wth->browser_get($base_url);
    lock_locale_url();

    comment("We never go past 1988-12-31 later, so these pledges stay");
    comment("neither failures nor successes until the end.");
}

#############################################################################
sub do_byarea_tests {
    set_pb_date('1984-09-01');
    lock_locale_url($base_url);

    comment("Make a by area pledge");
    my $pledge_action_boo = 'start a group for people who like to shout "boo!"';
    my $pledge_ref_boo = "boogroup";
    create_pledge(600, 
        { title => $pledge_action_boo, 
            target => '2', type => 'other surprisers from my town', signup => 'come to the first meeting',
            date => 'oct 8', ref => $pledge_ref_boo },
        { country => 'GB', 'local' => 0 }, 
        { },
        { action => "viaemail", setpassword => 1 }
    );
    set_pledge_prominence($pledge_ref_boo, 'frontpage');
    call_frequentupdate(); # update prominence cache
    comment("For now, make it a byarea one directly in the database");
    dbh()->do('update pledges set target_type = ? where ref = ?', {}, 'byarea', $pledge_ref_boo);
    dbh()->commit();

    comment("Sign it in a few places");
    find_and_sign_pledge("ref", $pledge_ref_boo, 601, 1, 
        {action=>"viaemail",setpassword=>1}, "signed", {byarea=>1});
    find_and_sign_pledge("ref", $pledge_ref_boo, 602, 1, 
        {action=>"viaemail",setpassword=>1}, "signed", {byarea=>1, byarea_near=>'new'});
    find_and_sign_pledge("ref", $pledge_ref_boo, 603, 1, 
        {action=>"viaemail",setpassword=>1}, "signed", {byarea=>1});
    call_frequentupdate();
    $wth->email_check_none_left();

    comment("This will provoke success in Liverpool");
    find_and_sign_pledge("ref", $pledge_ref_boo, 604, 1, 
        {action=>"viaemail",setpassword=>1}, "signed", {byarea=>1, byarea_near=>'default'});
    call_frequentupdate();
    my $success_email = $wth->email_get_containing( '%To: "' . name_n(600) . '" <' . email_n(600) . '>%We are pleased to tell you that your pledge%Liverpool%');
    die "Success email link not found\n" if ($success_email !~ m#^\s*(http://.*$)#m);
    my $announce_link = $1;
    display_url("Announce Liverpool URL", $announce_link);
    $wth->email_get_containing( '%To: "' . name_n(601) . '" <' . email_n(601) . '>%We are pleased to tell you that the pledge%Liverpool%');
    $wth->email_get_containing( '%To: "' . name_n(604) . '" <' . email_n(604) . '>%We are pleased to tell you that the pledge%Liverpool%');
    $wth->email_check_none_left();
    
    comment("Send success announce to Liverpool");
    $wth->browser_get($announce_link);
    $wth->browser_check_contents("Send success announcement message for Liverpool");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "It all works in Liverpool. Go to it guys. " . name_n(600) },
        button => 'submit') or die "Failed to submit Liverpool email success form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge in Liverpool");
    call_frequentupdate();
    $wth->email_get_containing( '%To: "' . name_n(601) . '" <' . email_n(601) . '>%From: "' . name_n(600) . '"%It all works in Liverpool%');
    $wth->email_get_containing( '%To: "' . name_n(604) . '" <' . email_n(604) . '>%From: "' . name_n(600) . '"%It all works in Liverpool%');
    $wth->email_get_containing( '%To: "' . name_n(600) . '" <' . email_n(600) . '>%From: '.$name_admin.  '%[ This is a copy of the message%It all works in Liverpool%');

    comment("This will provoke success in Birkenhead");
    find_and_sign_pledge("ref", $pledge_ref_boo, 605, 1, 
        {action=>"viaemail",setpassword=>1}, "signed", {byarea=>1, byarea_near=>'default'});
    call_frequentupdate();
    my $success_email_1 = $wth->email_get_containing( '%To: "' . name_n(600) . '" <' . email_n(600) . '>%We are pleased to tell you that your pledge%Birkenhead%');
    die "Success email link not found\n" if ($success_email_1 !~ m#^\s*(http://.*$)#m);
    my $announce_link_1 = $1;
    display_url("Announce Birkenhead URL", $announce_link_1);
    $wth->email_get_containing( '%To: "' . name_n(602) . '" <' . email_n(602) . '>%We are pleased to tell you that the pledge%Birkenhead%');
    $wth->email_get_containing( '%To: "' . name_n(605) . '" <' . email_n(605) . '>%We are pleased to tell you that the pledge%Birkenhead%');
    $wth->email_check_none_left();

    comment("Check display on pledge page");
    $wth->browser_get($locale_url . "/$pledge_ref_boo");
    $wth->browser_check_contents("This pledge has been successful in <strong>2 places</strong>!");

    comment("Success reminder messages");
    set_pb_date('1984-09-10');
    call_frequentupdate();
    $success_email_1 = $wth->email_get_containing( '%To: "' . name_n(600) . '" <' . email_n(600) . '>%Your pledge reached its target in Birkenhead three days ago%');
    die "Success reminder email link not found\n" if ($success_email_1 !~ m#^\s*(http://.*$)#m);
    my $announce_link_2 = $1;

    comment("Send success announce to Birkenhead");
    $wth->browser_get($announce_link_2);
    $wth->browser_check_contents("Send success announcement message for Birkenhead");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "It all rocks in Birkenhead. Go go to it guys. " . name_n(600) },
        button => 'submit') or die "Failed to submit Birkenhead email success form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge in Birkenhead");
    call_frequentupdate();
    $wth->email_get_containing( '%To: "' . name_n(602) . '" <' . email_n(602) . '>%From: "' . name_n(600) . '"%It all rocks in Birkenhead%');
    $wth->email_get_containing( '%To: "' . name_n(605) . '" <' . email_n(605) . '>%From: "' . name_n(600) . '"%It all rocks in Birkenhead%');
    $wth->email_get_containing( '%To: "' . name_n(600) . '" <' . email_n(600) . '>%From: '.$name_admin.  '%[ This is a copy of the message%It all rocks in Birkenhead%');

    comment("Cause failure of pledge for other towns");
    set_pb_date('1984-10-10');
    call_frequentupdate();
    my $failure_email = $wth->email_get_containing('%Subject: Your pledge%unsuccessful in Hexham%To: "' . name_n(600) . '" <' . email_n(600) . '>%');
    die "Failure email link not found\n" if ($failure_email !~ m#^\s*(http://.*$)#m);
    my $announce_link_failure = $1;
    display_url("Announce failure URL", $announce_link_failure);
    $wth->email_get_containing('%Subject: Your pledge%To: "' . name_n(603) . '" <' . email_n(603) . '>%'); # failed

    comment("Send failure announce to Hexham places");
    $wth->browser_get($announce_link_failure);
    $wth->browser_check_contents("Send failure announcement message for Hexham");
    $wth->browser_submit_form(form_name => 'pledge',
                fields => { message_body => "Boohoo! It went badly in Hexham. I'm very sorry." . name_n(600) },
        button => 'submit') or die "Failed to submit Hexham email failure form";
    $wth->browser_check_contents("Your message will now be sent to all the people who signed your pledge in Hexham");
    call_frequentupdate();
    $wth->email_get_containing( '%To: "' . name_n(603) . '" <' . email_n(603) . '>%From: "' . name_n(600) . '"%It went badly in Hexham%');
    $wth->email_get_containing( '%To: "' . name_n(600) . '" <' . email_n(600) . '>%From: '.$name_admin.'%[ This is a copy of the message%It went badly in Hexham%');

    $wth->browser_follow_link(text_regex => qr/this isn't you/);
    $wth->email_check_none_left();
}

#############################################################################
sub do_admin_tests {
    comment("Send contact message");
    $wth->browser_get($locale_url);
    $wth->browser_follow_link(text_regex => qr/About/);
    $wth->browser_follow_link(text_regex => qr/contact us/);
    $wth->browser_check_contents("Was it useful?");
    $wth->browser_submit_form(form_name => 'contact',
            fields => { 
                name => name_n(1000), 
                e => email_n(1000), 
                subject => "Lovely site", 
                message => "Hey, your PledgeBank site rules!" },
            button => 'submit') or die "Failed to submit contact form";
    $wth->browser_check_contents("Thanks for your feedback");
    $wth->email_get_containing(
            '%Subject: Lovely site' . 
            '%To: '.$contact_email.  
            '%From: "'. name_n(1000) . '"%rules!%');

    comment("Submit abusive comment report");
    $wth->browser_get($locale_url . "/automatedtest");
    $wth->browser_follow_link(text_regex => qr/Abusive/, n => 1);
    $wth->browser_check_contents("Report abusive, suspicious or wrong comment");
    $wth->browser_submit_form(form_name => 'contact',
            fields => { 
                name => name_n(1000),
                e => email_n(1000),
                subject => "Sucky sucky comment",
                message => "I hate this comment, it sucks",
                },
            button => 'submit') or die "Failed to submit comment abuse form";
    $wth->browser_check_contents("One of our team will investigate");
    $wth->email_get_containing('%To: '.$contact_email.  '%I hate this comment, it sucks%');

    comment("Check main admin page");
    $wth->browser_get($admin_url);
    $wth->browser_follow_link(text_regex => qr/Main Admin/);
    $wth->browser_check_contents("portuguese");
    $wth->browser_follow_link(text_regex => qr/All Closed Pledges/);
    $wth->browser_check_contents("automatedtest");
    $wth->browser_check_contents("privatepledge");
    $wth->browser_follow_link(text_regex => qr/admin/);
    $wth->browser_check_contents("This really is forever");

    comment("Check timeline page");
    $wth->browser_get($admin_url);
    $wth->browser_follow_link(text_regex => qr/Timeline/);
    $wth->browser_check_contents("10th October 1984");

    comment("Delete a signer");
    $wth->browser_get($admin_url . "/?page=pb&pledge=smsspoof");
    $wth->browser_check_contents("pretend to be an SMS aggregating service");
    $wth->browser_check_contents(mobile_n(105));
    $wth->browser_check_contents(name_n(105));
    $wth->browser_submit_form(form_name => 'removesignerform1',
            fields => { }, button => 'remove_signer') or die "Failed to remove signer";
    $wth->browser_check_contents("signer has been successfully removed");
    $wth->browser_check_contents("pretend to be an SMS aggregating service");
    $wth->browser_check_no_contents(mobile_n(105));
    $wth->browser_check_no_contents(email_n(105));

    comment("Delete a pledge");
    $wth->browser_get($admin_url . "/?page=pb&pledge=smsspoof");
    $wth->browser_submit_form(form_name => 'removepledgepermanentlyform',
            fields => { }, button => 'remove_pledge') or die "Failed to remove pledge";
    $wth->browser_check_contents("pledge has been successfully removed");
    $wth->browser_check_no_contents("smsspoof");
}

#############################################################################
sub do_final_checks {
    comment("Change to UK");
    $wth->browser_get($base_url . "/where"); # Could be any country
    $wth->browser_follow_link(text => "United Kingdom");

    comment("Check RSS works and has vaguely correct entries");
    $wth->browser_get($locale_url . "/rss");
    $wth->browser_check_contents(qr/make multiple pledges using the magic of login/);
    $wth->browser_check_no_contents("privatepledge");
}

